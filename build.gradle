plugins {
    id 'fabric-loom' version "${loomVersion}-SNAPSHOT"
}

Properties localProperties = new Properties()
localProperties.load(project.rootProject.file('local.properties').newDataInputStream())

def hintlyHallowsFormId = localProperties.getProperty('HINTLY_HALLOWS_FORM_ID')
def hintlyHallowsEntryId = localProperties.getProperty('HINTLY_HALLOWS_ENTRY_ID')

def modId = project.modName.toLowerCase()
def repoUrl = "https://github.com/${author}/${modName}"

version = "mc${minecraftVersion}+${modVersion}"
group = "com." + project.author

base {
    archivesName = modId
}

repositories {
    mavenCentral()
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraftVersion}"
    mappings "net.fabricmc:yarn:${minecraftVersion}+build.${mappingsBuild}:v2"
    modImplementation "net.fabricmc:fabric-loader:${minLoaderVersion}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${apiVersion}+${minecraftVersion}"
}

processResources {
    def propertyMap = [
            'modId'           : modId,
            'modVersion'      : modVersion,
            'modName'         : modName,
            'author'          : author,
            'repoUrl'         : repoUrl,
            'minJavaVersion'  : minJavaVersion,
            'minecraftVersion': minecraftVersion,
            'minLoaderVersion': minLoaderVersion
    ]
    inputs.properties(propertyMap)
    filesMatching("fabric.mod.json") {
        expand(propertyMap)
    }
}

sourceSets {
    main {
        resources.srcDirs += ['src/main/generated']
        java.srcDirs += ['src/main/generated/java']
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = 'UTF-8'
    it.options.release.set(minJavaVersion.toInteger())
}

tasks.register('generateBuildConfig', DefaultTask) {
    doLast {
        def templateFile = file("$projectDir/gradle/BuildConfig.java.txt")
        def configFile = file("$projectDir/src/main/generated/java/com/${author}/${modId}/BuildConfig.java")
        configFile.getParentFile().mkdirs()

        configFile.text = templateFile.text
                .replace('${author}', author)
                .replace('${modId}', modId)
                .replace('${modName}', modName)
                .replace('${isDebug}', isDebug)
                .replace('${hintlyHallowsFormId}', hintlyHallowsFormId)
                .replace('${hintlyHallowsEntryId}', hintlyHallowsEntryId)
    }
}

tasks.register('packBuildDebug', Exec) {
    commandLine 'sh', "$projectDir/build_pack.sh",
            modId,
            modVersion,
            modName,
            author,
            repoUrl,
            minecraftVersion,
            minLoaderVersion,
            true
}

tasks.register('packBuildRelease', Exec) {
    commandLine 'sh', "$projectDir/build_pack.sh",
            modId,
            modVersion,
            modName,
            author,
            repoUrl,
            minecraftVersion,
            minLoaderVersion,
            false
}

java {
    sourceCompatibility = targetCompatibility = minJavaVersion.toInteger()
}

jar {
    from('LICENSE') {
        rename { "${it}_${modId}" }
    }
    compileJava.dependsOn(generateBuildConfig)
}

loom {
    accessWidenerPath.set(file("src/main/resources/${modId}.accesswidener"))

    runs {
        datagen {
            inherit server
            name "Data Generation"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=${modId}"

            runDir "build/datagen"
        }
    }
}
